# ==============================
# START
# ==============================

<START> ::= <structOrImplOrFunction> <START>
<START> ::= EPSILON 

<structOrImplOrFunction> ::= <structDeclaration> 
<structOrImplOrFunction> ::= <implDefinition> 
<structOrImplOrFunction> ::= <functionDefinition> 

# ==============================
# STRUCT
# ==============================

<structDeclaration> ::= 'struct' 'id' <structOptionalInheritance> '{' <repetitiveStructMemberDeclarations> '}' ';' 

<structOptionalInheritance> ::= 'inherits' 'id' <repetitiveStructOptionalInheritances> 
<structOptionalInheritance> ::= EPSILON 

<repetitiveStructMemberDeclarations> ::= <visibility> <memberDeclaration> <repetitiveStructMemberDeclarations> 
<repetitiveStructMemberDeclarations> ::= EPSILON 

<repetitiveStructOptionalInheritances> ::= ',' 'id' <repetitiveStructOptionalInheritances> 
<repetitiveStructOptionalInheritances> ::= EPSILON 

<memberDeclaration> ::= <functionDeclaration> 
<memberDeclaration> ::= <variableDeclaration> 

<functionDeclaration> ::= <functionHeader> ';' 

# ==============================
# IMPL
# ==============================

<implDefinition> ::= 'impl' 'id' '{' <repetitiveFunctionDefinitions> '}' 

<repetitiveFunctionDefinitions> ::= <functionDefinition> <repetitiveFunctionDefinitions> 
<repetitiveFunctionDefinitions> ::= EPSILON 


# ==============================
# FUNCTION
# ==============================

<functionDefinition> ::= <functionHeader> <functionBody> 

<functionHeader> ::= 'func' 'id' '(' <funtionParameters> ')' '->' <returnType> 

<funtionParameters> ::= 'id' ':' <type> <repetitiveArraySizes> <repetitiveFunctionParametersTails> 
<funtionParameters> ::= EPSILON 

<repetitiveFunctionParametersTails> ::= <functionParametersTail> <repetitiveFunctionParametersTails> 
<repetitiveFunctionParametersTails> ::= EPSILON 

<functionParametersTail> ::= ',' 'id' ':' <type> <repetitiveArraySizes> 

<functionBody> ::= '{' <repetitiveVariableDeclarationOrStatements> '}' 

<repetitiveVariableDeclarationOrStatements> ::= <variableDeclarationOrStatement> <repetitiveVariableDeclarationOrStatements> 
<repetitiveVariableDeclarationOrStatements> ::= EPSILON 

<variableDeclarationOrStatement> ::= <variableDeclaration> 
<variableDeclarationOrStatement> ::= <statement> 

# -----------------------------
# Array Size
# -----------------------------

<arraySize> ::= '[' <arraySizeContent> 

<arraySizeContent> ::= 'intNum' ']' 
<arraySizeContent> ::= ']' 

<repetitiveArraySizes> ::= <arraySize> <repetitiveArraySizes> 
<repetitiveArraySizes> ::= EPSILON 


# -----------------------------
# Variable Declaration
# -----------------------------

<variableDeclaration> ::= 'let' 'id' ':' <type> <repetitiveArraySizes> ';' 

# -----------------------------
# Statement
# -----------------------------

<statement> ::= 'id' <statementAlt> ';'
<statement> ::= 'if' '(' <relationalExpression> ')' 'then' <statementBlock> 'else' <statementBlock> ';' 
<statement> ::= 'while' '(' <relationalExpression> ')' <statementBlock> ';' 
<statement> ::= 'read' '(' <variable> ')' ';' 
<statement> ::= 'write' '(' <expression> ')' ';' 
<statement> ::= 'return' '(' <expression> ')' ';' 

<statementBlock> ::= '{' <repetitiveStatements> '}' 
<statementBlock> ::= <statement> 
<statementBlock> ::= EPSILON 

# <optionalAssignment> ::= <assignmentOperator> <expression> ';'
# <optionalAssignment> ::= ';'

<repetitiveStatements> ::= <statement> <repetitiveStatements> 
<repetitiveStatements> ::= EPSILON 

# <statementAlt> ::= '(' <argumentParameters> ')' 
# <statementAlt> ::= <repetitiveIndices> 
# <statementAlt> ::= EPSILON

# <repetitiveVariableOrFunctionCallStat> ::= <idnestStat> <repetitiveVariableOrFunctionCallStat> 
# <repetitiveVariableOrFunctionCallStat> ::= EPSILON 

# <idnestStat> ::= '.' 'id' <idnestRestStat> 

# <idnestRestStat> ::= '(' <argumentParameters> ')' 
# <idnestRestStat> ::= <repetitiveIndices> 
# <idnestRestStat> ::= EPSILON



<statementAlt> ::= '(' <argumentParameters> ')' <repetitiveVariableOrFunctionCallStat_Function> 
<statementAlt> ::= <repetitiveIndicesStat> 

<repetitiveIndicesStat> ::= <indice> <repetitiveIndicesStat> 
<repetitiveIndicesStat> ::= <repetitiveVariableOrFunctionCallStat_Var>


<repetitiveVariableOrFunctionCallStat_Function> ::= <idnestStat> 
<repetitiveVariableOrFunctionCallStat_Function> ::= EPSILON

<repetitiveVariableOrFunctionCallStat_Var> ::= <idnestStat> 
<repetitiveVariableOrFunctionCallStat_Var> ::= <assignmentOperator> <expression>

# <repetitiveVariableOrFunctionCallStat> ::= <idnestStat> <repetitiveVariableOrFunctionCallStat> 
# <repetitiveVariableOrFunctionCallStat> ::= EPSILON 

<idnestStat> ::= '.' 'id' <idnestRestStat> 

<idnestRestStat> ::= '(' <argumentParameters> ')' <repetitiveVariableOrFunctionCallStat_Function> 
<idnestRestStat> ::= <repetitiveIndicesStat> 

# -----------------------------
# VARIABLE
# -----------------------------

<variable> ::= 'id' <variableRest> 

<variableRest> ::= <repetitiveIndices> <repetitiveVariables> 
<variableRest> ::= '(' <argumentParameters> ')' <variableIdnest> 
<variableRest> ::= EPSILON
<variableRest> ::= <repetitiveVariables>

<variableIdnest> ::= '.' 'id' <variableIdnestRest> 

<variableIdnestRest> ::= '(' <argumentParameters> ')' <variableIdnest> 
<variableIdnestRest> ::= <repetitiveIndices> 
<variableIdnestRest> ::= EPSILON

<repetitiveVariables> ::= <variableIdnest> <repetitiveVariables> 
<repetitiveVariables> ::= EPSILON 

# -----------------------------
# EXPRESSION
# -----------------------------

<expression> ::= <arithmeticExpression> <optionalRelationalExpression> 

<arithmeticExpression> ::= <term> <recursiveArithmeticExpression> 

<relationalExpression> ::= <arithmeticExpression> <relationalOperator> <arithmeticExpression> 

<optionalRelationalExpression> ::= <relationalOperator> <arithmeticExpression> 
<optionalRelationalExpression> ::= EPSILON 

<recursiveArithmeticExpression> ::= <addOperator> <term> <recursiveArithmeticExpression> 
<recursiveArithmeticExpression> ::= EPSILON 

<term> ::= <factor> <recursiveTerms> 

<recursiveTerms> ::= <multOperator> <factor> <recursiveTerms> 
<recursiveTerms> ::= EPSILON 


# -----------------------------
# FACTOR
# -----------------------------

<factor> ::= 'id' <factorAlt> <repetitiveVariableOrFunctionCall> 
<factor> ::= 'intLit' 
<factor> ::= 'floatLit' 
<factor> ::= '(' <arithmeticExpression> ')' 
<factor> ::= 'not' <factor> 
<factor> ::= <sign> <factor> 

<factorAlt> ::= '(' <argumentParameters> ')' 
<factorAlt> ::= <repetitiveIndices> 
<factorAlt> ::= EPSILON

<repetitiveVariableOrFunctionCall> ::= <idnest> <repetitiveVariableOrFunctionCall> 
<repetitiveVariableOrFunctionCall> ::= EPSILON 

<idnest> ::= '.' 'id' <idnestRest> 

<idnestRest> ::= '(' <argumentParameters> ')' 
<idnestRest> ::= <repetitiveIndices> 
<idnestRest> ::= EPSILON


# -----------------------------
# INDICE
# -----------------------------

<indice> ::= '[' <arithmeticExpression> ']' 

<repetitiveIndices> ::= <indice> <repetitiveIndices> 
<repetitiveIndices> ::= EPSILON 


# -----------------------------
# ARGUMENT PARAMS
# -----------------------------

<argumentParameters> ::= <expression> <repetitiveArgumentParametersTail> 
<argumentParameters> ::= EPSILON 

<argumentParametersTail> ::= ',' <expression> 

<repetitiveArgumentParametersTail> ::= <argumentParametersTail> <repetitiveArgumentParametersTail> 
<repetitiveArgumentParametersTail> ::= EPSILON 

# -----------------------------
# CONSTANTS
# -----------------------------

<returnType> ::= <type> 
<returnType> ::= 'void' 

<type> ::= 'integer' 
<type> ::= 'float' 
<type> ::= 'id' 

<visibility> ::= 'public' 
<visibility> ::= 'private'

<addOperator> ::= '+' 
<addOperator> ::= '-' 
<addOperator> ::= 'or' 

<assignmentOperator> ::= '=' 

<sign> ::= '+' 
<sign> ::= '-' 

<relationalOperator> ::= 'eq' 
<relationalOperator> ::= 'neq' 
<relationalOperator> ::= 'lt' 
<relationalOperator> ::= 'gt' 
<relationalOperator> ::= 'leq' 
<relationalOperator> ::= 'geq' 

<multOperator> ::= '*' 
<multOperator> ::= '/' 
<multOperator> ::= 'and' 